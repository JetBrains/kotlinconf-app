buildscript {
    apply from: "versions.gradle"

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "http://dl.bintray.com/kotlin/kotlin-dev" }
        mavenCentral()
    }
    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

group "org.jetbrains.kotlinconf"
version "1.0-SNAPSHOT"

apply plugin: "kotlin2js"
apply plugin: "kotlin-dce-js"
apply plugin: "com.moowork.node"

node {
    download = true
}

repositories {
    maven { url "http://dl.bintray.com/kotlin/kotlin-dev" }
    maven { url "http://dl.bintray.com/kotlinx/kotlinx" }
    maven { url "http://dl.bintray.com/hypnosphi/kotlin-wrappers" }
    mavenCentral()
}

dependencies {
    compile project(":common-js")
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile("org.jetbrains.kotlinx:kotlinx-html-js:$kotlinx_html_version") { force = true }
    compile "org.jetbrains:kotlin-react:$react_version"
    compile "org.jetbrains:kotlin-react-dom:$react_dom_version"
}

compileKotlin2Js {
    kotlinOptions {
        sourceMap = true
        sourceMapEmbedSources = "always"
        moduleKind = 'commonjs'
        metaInfo = false
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

task copyStatic(type: Copy) {
    from "$rootDir/src/main/web"
    into "$buildDir/web"
}

task buildBundle(type: NpmTask, dependsOn: [npmInstall, runDceKotlinJs]) {
    args = ["run", "dist"]
}

task copyKotlinJs(type: Copy, dependsOn: compileKotlin2Js) {
    def workDir = "$buildDir/classes/main/"
    from(workDir) {
        include "*.js"
        include "*.js.map"
    }
    into "$workDir/dependencies"
}

task devBuild(dependsOn: [npmInstall, copyStatic, copyKotlinJs])

assemble.dependsOn buildBundle, copyStatic

afterEvaluate {
    copyKotlinJs.dependsOn copyDependenciesKotlinJs
}